plugins {
    id "java"
    id "application"
    id "org.jetbrains.kotlin.jvm" version "1.3.70" apply false
    id "org.jetbrains.kotlin.kapt" version "1.3.70" apply false
    id "org.jetbrains.kotlin.plugin.allopen" version "1.3.70" apply false
    id "com.google.cloud.tools.jib" version '2.1.0' apply false
    id "com.tngtech.jgiven.gradle-plugin" version "1.0.0-RC4" apply false
}

subprojects {

    repositories {
        mavenLocal()
        mavenCentral()
    }

    ext {
        micronaut_version = "1.3.2"
    }

    ext.libs = [
            unit_tests      : [
                    "org.junit.jupiter:junit-jupiter:5.+",
                    "com.nhaarman:mockito-kotlin:1.6.+",
                    "org.mockito:mockito-inline:3.3.+"
            ],

            acceptance_tests: [
                    "com.tngtech.jgiven:jgiven-junit5:0.18.+"
            ],

            integration_test: [
                    "io.micronaut.test:micronaut-test-junit5:1.1.+",
                    "com.github.kstyrc:embedded-redis:0.6"
            ],

            micronaut       : [
                    "io.micronaut:micronaut-runtime:${micronaut_version}",
                    "io.micronaut:micronaut-core:${micronaut_version}",
                    "io.micronaut:micronaut-inject-java:${micronaut_version}",
                    "io.micronaut:micronaut-validation:${micronaut_version}"
            ],

            database        : [
                    "io.micronaut.configuration:micronaut-redis-lettuce:1.2.0"
            ],

            rest            : [
                    "io.micronaut:micronaut-http-server-netty:${micronaut_version}",
                    "io.micronaut:micronaut-http-client:${micronaut_version}"
            ],

            rpc             : [

            ],

            log             : [
                    "ch.qos.logback:logback-classic:1.2.3"
            ],

            commons         : [
                    "commons-validator:commons-validator:1.6",
                    "commons-codec:commons-codec:1.14"
            ],

            kapt_micronaut  : [
                    "io.micronaut:micronaut-inject-java:${micronaut_version}",
                    "io.micronaut:micronaut-validation:${micronaut_version}"
            ]
    ]

    apply plugin: "java"
    apply plugin: "org.jetbrains.kotlin.jvm"
    apply plugin: "org.jetbrains.kotlin.kapt"
    apply plugin: "org.jetbrains.kotlin.plugin.allopen"

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        implementation "org.jetbrains.kotlin:kotlin-reflect"
    }

    allOpen {
        annotation("io.micronaut.aop.Around")
    }

    compileKotlin {
        kotlinOptions {
            // We are still using 1.8 due to poor support for modules from libraries we depend.
            jvmTarget = '1.8'
            // Will retain parameter names for Java reflection
            javaParameters = true
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = '1.8'
            javaParameters = true
        }
    }

    test {
        useJUnitPlatform()
    }
}